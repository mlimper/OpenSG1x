# Contrib

Import('*')

ContribDirs = []

if _po.getOption('contrib_cgchunk'):
    if _po.getOption('cg'):
        ContribDirs.append(Dir('CGChunk'))
    else:
        print 'Contrib CGChunk needs cg lib, add cg=<path> to cmd line.'

if _po.getOption('contrib_cgfxmaterial'):
    if _po.getOption('cg'):
        ContribDirs.append(Dir('CGFXMaterial'))
    else:
        print 'Contrib CGFXMaterial needs cgfx lib, add cg=<path> to cmd line.'

if _po.getOption('contrib_cgfxmaterial2'):
    if _po.getOption('cg'):
        ContribDirs.append(Dir('CGFXMaterial2'))
    else:
        print 'Contrib CGFXMaterial2 needs cgfx lib, add cg=<path> to cmd line.'

if _po.getOption('contrib_ply'):
    ContribDirs.append(Dir('PLY'))

if _po.getOption('contrib_drawfunctorcore'):
    ContribDirs.append(Dir('DrawFunctorCore'))

if _po.getOption('contrib_terrain'):
    ContribDirs.append(Dir('Terrain'))

headers, sources, tests, generated = ProcessDirs(ContribDirs)
# don't forget the OSGContribDef.h file.
headers += Glob('OSG*.h')
InstallHeaders(env, headers)

def custom(env):

    if (_po.getOption('contrib_cgchunk') or _po.getOption('contrib_cgfxmaterial')) and _po.getOption('cg'):
        env.Append(CPPPATH=env.get('CGCPPPATH'))
        env.Append(LIBPATH=env.get('CGLIBPATH'))
        env.Append(LIBS=env.get('OSG_CG_LIBS'))
    env.Append(LIBS=env.get('OSG_SYSTEM_LIBS'))

if len(sources) > 0:
    BuildLibrary(tc, 'OSGContrib', sources,
                 updates=[custom, OpenSGLibrary(['OSGBase', 'OSGSystem'])],
                 CPPDEFINES=['OSG_BUILD_DLL', 'OSG_COMPILECONTRIBLIB'],
                 CPPPATH=ExpandCPPPath([Dir('.')] + SystemDirs + BaseDirs + ContribDirs, generated))
